/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

// This is based off:
// https://github.com/mozilla/application-services/blob/c40e2ccb422cf4af9ffdf095149cec34de1d4bef/components/fxa-client/android/build.gradle

plugins {
    id "com.jetbrains.python.envs" version "0.0.26"
}

apply plugin: 'com.android.library'
apply plugin: 'org.mozilla.rust-android-gradle.rust-android'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

/*
 * This defines the location of the JSON schema used to validate the pings
 * created during unit testing.
 * This uses a specific version of the schema identified by a git commit hash.
 */
String GLEAN_PING_SCHEMA_GIT_HASH = "64b852c"
String GLEAN_PING_SCHEMA_URL = "https://raw.githubusercontent.com/mozilla-services/mozilla-pipeline-schemas/$GLEAN_PING_SCHEMA_GIT_HASH/schemas/glean/baseline/baseline.1.schema.json"

// Set configuration for the glean_parser
ext.allowGleanInternal = true
ext.gleanNamespace = "mozilla.telemetry.glean"

android {
    compileSdkVersion rootProject.ext.build.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.build['minSdkVersion']
        targetSdkVersion rootProject.ext.build['targetSdkVersion']

        // TODO: 1551691 Get the version from git tag...? Also, we need to select a
        // version that won't conflict with legacy glean-ac versions.
        buildConfigField("String", "LIBRARY_VERSION", "\"0.1\"")
        buildConfigField("String", "GLEAN_PING_SCHEMA_URL", "\"" + GLEAN_PING_SCHEMA_URL + "\"")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles "$rootDir/proguard-rules-consumer-jna.pro"
        }

        withoutLib {
            initWith release
        }
    }

    sourceSets {
        main.jniLibs.srcDirs += "$buildDir/nativeLibs/android"
        test.resources.srcDirs += "$buildDir/rustJniLibs/desktop"
        test.resources.srcDirs += "$buildDir/nativeLibs/desktop"
    }

    // Uncomment to include debug symbols in native library builds.
    // packagingOptions { doNotStrip "**/*.so" }
}

configurations {
    withoutLib {
    }
}

afterEvaluate {
    android.sourceSets.debug.jniLibs.srcDirs = android.sourceSets.main.jniLibs.srcDirs
    android.sourceSets.release.jniLibs.srcDirs = android.sourceSets.main.jniLibs.srcDirs
    android.sourceSets.main.jniLibs.srcDirs = []
}

cargo {
    // The directory of the Cargo.toml to build.
    module = '../ffi'

    // The Android NDK API level to target.
    apiLevel = 21

    // Where Cargo writes its outputs.
    targetDirectory = '../../target'

    libname = 'glean_ffi'

    targets = rootProject.ext.rustTargets

    profile = rootProject.ext.nonMegazordProfile

    exec = rootProject.ext.cargoExec

    extraCargoBuildArguments = rootProject.ext.extraCargoBuildArguments
}

configurations {
    // There's an interaction between Gradle's resolution of dependencies with different types
    // (@jar, @aar) for `implementation` and `testImplementation` and with Android Studio's built-in
    // JUnit test runner.  The runtime classpath in the built-in JUnit test runner gets the
    // dependency from the `implementation`, which is type @aar, and therefore the JNA dependency
    // doesn't provide the JNI dispatch libraries in the correct Java resource directories.  I think
    // what's happening is that @aar type in `implementation` resolves to the @jar type in
    // `testImplementation`, and that it wins the dependency resolution battle.
    //
    // A workaround is to add a new configuration which depends on the @jar type and to reference
    // the underlying JAR file directly in `testImplementation`.  This JAR file doesn't resolve to
    // the @aar type in `implementation`.  This works when invoked via `gradle`, but also sets the
    // correct runtime classpath when invoked with Android Studio's built-in JUnit test runner.
    // Success!
    jnaForTest
}

android {
  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

dependencies {
    api "org.mozilla.components:concept-fetch:$android_components_version"
    implementation "org.mozilla.components:lib-fetch-httpurlconnection:$android_components_version"
    implementation "org.mozilla.components:support-ktx:$android_components_version"

    jnaForTest "net.java.dev.jna:jna:$jna_version@jar"
    implementation "net.java.dev.jna:jna:$jna_version@aar"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1"

    implementation "androidx.annotation:annotation:1.0.2"
    implementation "androidx.work:work-runtime-ktx:2.0.0"

    // For reasons unknown, resolving the jnaForTest configuration directly
    // trips a nasty issue with the Android-Gradle plugin 3.2.1, like `Cannot
    // change attributes of configuration ':PROJECT:kapt' after it has been
    // resolved`.  I think that the configuration is being made a
    // super-configuration of the testImplementation and then the `.files` is
    // causing it to be resolved.  Cloning first dissociates the configuration,
    // avoiding other configurations from being resolved.  Tricky!
    testImplementation files(configurations.jnaForTest.copyRecursive().files)
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.2.1'
    testImplementation 'org.mockito:mockito-core:2.24.5'
    testImplementation 'androidx.test:core-ktx:1.1.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
    testImplementation "org.mozilla.components:lib-fetch-okhttp:$android_components_version"
    testImplementation "org.mozilla.components:support-test:$android_components_version"
    testImplementation "androidx.work:work-testing:2.0.0"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

afterEvaluate {
    // The `cargoBuild` task isn't available until after evaluation.
    android.libraryVariants.all { variant ->
        def productFlavor = ""
        variant.productFlavors.each {
            productFlavor += "${it.name.capitalize()}"
        }
        def buildType = "${variant.buildType.name.capitalize()}"
        if (variant.buildType.name != 'withoutLib') {
            tasks["generate${productFlavor}${buildType}Assets"].dependsOn(tasks["cargoBuild"])
        }

        // For unit tests.
        tasks["process${productFlavor}${buildType}UnitTestJavaRes"].dependsOn(tasks["cargoBuild"])
    }
}

apply from: "$rootDir/publish.gradle"
// If using jnaForTestConfiguration or variantWithoutLib,
// please also update the corresponding .buildconfig-android.yml
// `publishedArtifacts` property.
ext.configurePublish(
        /* jnaForTestConfiguration= */ configurations.jnaForTest,
        /* variantWithoutLib= */ 'androidWithoutLib',
)

task docs(type: org.jetbrains.dokka.gradle.DokkaAndroidTask, overwrite: true) {
    moduleName = "glean"
    outputDirectory = "$buildDir/../../../build/docs/javadoc"
    outputFormat = "html"
    jdkVersion = 7
}

//apply from: 'https://github.com/mozilla-mobile/android-components/raw/master/components/service/glean/scripts/sdk_generator.gradle'
apply from: 'sdk_generator.gradle'
