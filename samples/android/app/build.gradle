/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

plugins {
    alias libs.plugins.gradle.python.envs
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'

kotlin {
    jvmToolchain(rootProject.ext.build.jvmTargetCompatibility)
}

android {
    namespace = "org.mozilla.samples.gleancore"
    compileSdkVersion rootProject.ext.build.compileSdkVersion

    defaultConfig {
        applicationId "org.mozilla.samples.gleancore"
        minSdkVersion rootProject.ext.build['minSdkVersion']
        targetSdkVersion rootProject.ext.build['targetSdkVersion']
        // Keep the version in sync with gleanExpireByVersion which is
        // set below, near the end of the file.
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }
}

dependencies {
    implementation project(':glean')

    implementation libs.androidx.appcompat
    implementation libs.androidx.browser

    androidTestImplementation libs.mockwebserver
    androidTestImplementation libs.test.core
    androidTestImplementation libs.test.espresso.core
    androidTestImplementation libs.test.junit.ext
    androidTestImplementation libs.test.rules
    androidTestImplementation libs.test.runner
    androidTestImplementation libs.test.uiautomator
    androidTestImplementation libs.test.work
}

ext.gleanNamespace = "mozilla.telemetry.glean"
// Fixed build date so we can test for it
ext.gleanBuildDate = "2020-11-06T11:30:50+00:00"
// Enable expiration by major version. The sample is at version 1,
// see versionName.
ext.gleanExpireByVersion = 1

// Include the glean-gradle-plugin. This is slightly different than what is
// recommended for external users since we are loading it from the same root Gradle
// build.
apply from: '../../../gradle-plugin/src/main/groovy/mozilla/telemetry/glean-gradle-plugin/GleanGradlePlugin.groovy'
ext.glean_plugin.apply(project)
